package OnUnitEnterLeave

import MapBounds
import NoWurst
import Unit
import Trigger
import Player
import TempGroups
import AbilityObjEditing
import initlater RegisterEvents

/**
 * Rawcode for the undefend ability to detect leavers
 */
constant int LEAVER_ABILITY_ID = '!e@$'

/**
 * Unit who fired etheir the enter or leave event
 */
unit triggerUnit = null

/**
 * "events"
 */
constant trigger onEnterTrigger = CreateTrigger()
constant trigger onLeaveTrigger = CreateTrigger()

/**
 * Get the unit who fired the event
 */
public function getEnterLeaveUnit() returns unit
	return triggerUnit

/**
 * Register a code to fire when a unit enters the map
 */	
public function onEnter(code c)
	onEnterTrigger.addCondition(Condition(c))

/**
 * Register a code to fire when a unit leaves the map
 */
public function onLeave(code c)
	onLeaveTrigger.addCondition(Condition(c))

/**
 * Check if the unit has the leaver ability (true if it does)
 */
function hasLeaverDetector(unit whichUnit) returns boolean
	return whichUnit.getAbilityLevel(LEAVER_ABILITY_ID) != 0
	
/**
 * Add leaver ability to unit (false if already has it)
 */
function addLeaverDetector(unit whichUnit) returns boolean
	if (hasLeaverDetector(whichUnit) == false)
		whichUnit.addAbility(LEAVER_ABILITY_ID)
		whichUnit.makeAbilityPermanent(LEAVER_ABILITY_ID, true)
		
		return true
	
	return false

/**
 * Take the unit and fire enter or leave event
 */
function fireEvent(unit whichUnit, trigger whichEvent)
	unit prevTriggerUnit = triggerUnit
	
	triggerUnit = whichUnit
	whichEvent.evaluate()
	triggerUnit = prevTriggerUnit
	
	prevTriggerUnit = null

function onEnterRegionEx() returns boolean
	addLeaverDetector(GetEnteringUnit())
	fireEvent(GetEnteringUnit(), onEnterTrigger)
	
	return false

/**
 * Add leaver detector to units who enter/were in the map and then fire
 * the entering event
 */	
function checkEnteringUnits()
	trigger enterTrigger = CreateTrigger()
	
	enterTrigger.registerEnterRegion(boundRegion, null)
	enterTrigger.addCondition(Condition(function onEnterRegionEx))
	
	GroupEnumUnitsInRect(ENUM_GROUP, boundRect, Condition(function onEnterRegionEx))
	
	enterTrigger = null

function checkLeaversEx()
	unit leaverUnit = GetFilterUnit()
	
	if (hasLeaverDetector(leaverUnit) == false)
		fireEvent(leaverUnit, onLeaveTrigger)
		
	leaverUnit = null

/**
 * Detect when a unit leaves and fire the event
 */
function checkLeavers()
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function checkLeaversEx)

function disableLeaverAbilityForPlayers()
	for i = 0 to bj_MAX_PLAYER_SLOTS-1
		players[i].setAbilityAvailable(LEAVER_ABILITY_ID, false)
	
init
	checkEnteringUnits()
	checkLeavers()
	disableLeaverAbilityForPlayers()    

@compiletime function generateLeaverAbility()
	AbilityDefinitionDefend leaverAbility = new AbilityDefinitionDefend(LEAVER_ABILITY_ID)
	
	leaverAbility.setName("Leaver Detect")
	leaverAbility.setArtCaster("")
	leaverAbility.setIconNormal("")
	leaverAbility.setRace(Race.Unknown)
	
